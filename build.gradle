plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

import org.gradle.internal.os.OperatingSystem

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    }
    else {
        def os = OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        }
        else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        }
        else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

if (!ext.has('hytaleHome')) {
    throw new GradleException('Your Hytale install could not be detected automatically. If you are on an unsupported platform or using a custom install location, please define the install location using the hytale_home property.');
}
else if (!file(ext.hytaleHome).exists()) {
    throw new GradleException("Failed to find Hytale at the expected location. Please make sure you have installed the game. The expected location can be changed using the hytale_home property. Currently looking in ${ext.hytaleHome}")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
    mavenCentral()
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))

}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def createServerRunArguments(String activePatchline = patchline) {
    // Seul Professions est déclaré dans --mods.
    // Economy et Persistence sont sur le classpath JVM (via implementation project) :
    // Hytale les découvre en scannant les manifest.json des ressources compilées,
    // évitant ainsi un double-chargement qui briserait l'association asset pack / plugin.
    def modPaths = [
        file("src/main/resources").absolutePath,
    ]
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/${activePatchline}/package/game/latest/Assets.zip" + '"'
    programParameters += ' --mods="' + modPaths.join(',') + '"'
    return programParameters
}

// Configuration du debug distant.
// Note : On utilise préférentiellement le bouton "Debug" d'IntelliJ sur les tâches standards.
def REMOTE_DEBUG_ARGS = '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'

// Configurations de lancement IntelliJ pour lancer le serveur avec tous les plugins FReZ.
idea.project.settings.runConfigurations {
    'Hytale Server (Release)'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments('release')
        workingDirectory = serverRunDir.absolutePath
    }
    'Hytale Server (Pre-Release)'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments('pre-release')
        workingDirectory = serverRunDir.absolutePath
    }
    // Utiliser le bouton RUN pour ces configurations (le debug est inclus dans les arguments JVM)
    'Hytale Server [Remote Attach] (Release)'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments('release')
        workingDirectory = serverRunDir.absolutePath
        jvmArgs = REMOTE_DEBUG_ARGS
    }
}

tasks.register('runServer', JavaExec) {
    group = 'Hytale'
    description = 'Lance le serveur Hytale. Utilisez le bouton DEBUG d\'IntelliJ pour débugger.'
    dependsOn 'classes'
    mainClass = 'com.hypixel.hytale.Main'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = serverRunDir
    standardInput = System.in
    args createServerRunArguments(patchline).split(' ')
}

tasks.register('runServerDebug', JavaExec) {
    group = 'Hytale'
    description = 'Lance le serveur Hytale avec le debug natif Gradle (port 5005).'
    dependsOn 'classes'
    mainClass = 'com.hypixel.hytale.Main'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = serverRunDir
    standardInput = System.in
    
    // Utilise le flag natif de Gradle pour éviter les doublons d'agent JDWP
    debug = true
    
    args createServerRunArguments(patchline).split(' ')
}

tasks.register('generateVSCodeLaunch') {
    def vscodeDir = file("$projectDir/.vscode")
    def launchFile = file("$vscodeDir/launch.json")
    doLast {
        if (!vscodeDir.exists()) {
            vscodeDir.mkdirs()
        }
        def programParams = createServerRunArguments(patchline)
        def launchConfig = [
            version: "0.2.0",
            configurations: [
                [
                    type: "java",
                    name: "HytaleServer",
                    request: "launch",
                    mainClass: "com.hypixel.hytale.Main",
                    args: programParams,
                    cwd: "\${workspaceFolder}/run"
                ]
            ]
        ]
        launchFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(launchConfig))
    }
}
